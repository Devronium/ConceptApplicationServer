import standard.arch.mp3
import standard.lib.str

class MP3Encoder {
	protected var encoder;
	protected var _buffer1 = "";
	protected var _buffer2 = "";
	protected var _channels = 1;
	protected var _samples;
	protected var _mp3 = "";

	public property Channels { get _channels }
	public property Data { get _mp3 }

	MP3Encoder(parameters = null) {
		if (!parameters)
			parameters = ["channels" => 1, "bitrate" => 16, "samplerate" => 8000];
		_channels = parameters["channels"];
		if (_channels < 1)
			_channels = 1;
		if (_channels > 2)
			_channels = 2;

		encoder = MP3EncoderCreate(parameters);

		_samples = MP3EncoderSamples(encoder) * _channels;
	}

	close() {
		if (encoder) {
			_mp3 += MP3EncoderFlush(encoder);
			MP3EncoderDone(encoder);
			encoder = null;
		}
	}

	clear() {
		_mp3 = "";
	}

	encode(var buf, var buf2 = "") {
		_buffer1 += buf;
		if (_channels > 1)
			_buffer2 += buf2;
		var buf_len = _samples * 2;
		var offset = 0;
		while ((length _buffer1 - offset) >= buf_len) {
			if (_channels > 1) {
				_mp3 += MP3EncoderEncode(encoder, SubStr(_buffer1, offset, buf_len), SubStr(_buffer2, offset, buf_len));
				offset += buf_len;
			} else {
				_mp3 += MP3EncoderEncode(encoder, SubStr(_buffer1, offset, buf_len));
				offset += buf_len;
			}
		}
		_buffer1 = SubStr(_buffer1, offset);
		_buffer2 = SubStr(_buffer2, offset);
	}

	finalize() {
		this.close();
	}
}
