import standard.C.io
import standard.lib.str
import standard.lib.whisper
import standard.lang.serialize
import standard.lib.samplerate
import standard.lib.vad
import standard.math.rand

include Worker.con

class WhisperTTSHelper {
	var modelName;

	WhisperTTSHelper(model_name) {
		this.modelName = model_name;
	}

	static tts(text, model, out_path, extra_params = "") {
		text = StrReplace(text, "\\", "\\\\");
		text = StrReplace(text, "\"", "\\\"");
		if (extra_params)
			extra_params = " " + extra_params;
		return _system("tts --text \"$text\" --model_name \"$model\" --out_path \"$out_path\"$extra_params");
	}

	speak(text, out_path) {
		return this.tts(text, modelName, out_path);
	}
}

class WhisperMessage {
	var id;
	var stream_id;
	var buffer;

	WhisperMessage(id, buffer = "", stream_id = 0) {
		this.id = id;
		this.buffer = buffer;
		this.stream_id = stream_id;
	}
}

class WhisperWorker {
	WhisperWorker(data) {
		var arr = UnBinarizeObject(data);
		var handle = arr[0];
		var id = arr[1];

		console.log(LOG_THIS, "created worker $id");
		while (true) {
			if (Worker::Pending(var buffer, 1000)) {
				var obj = UnBinarizeObject(buffer);
				if (obj) {
					if (obj.id < 0)
						break;

					obj.buffer = WhisperDecode(handle, obj.buffer);
					Worker::Result(BinarizeObject(obj));
				}
			}
		}
		console.log(LOG_THIS, "removed worker");
	}
}

class WhisperSpeechStream {
	protected var audio_buffer = "";
	protected var _started;
	protected var _start_voice;
	protected var _end_voice;
	protected var _owner;
	protected var _worker_id;
	protected var _worker;
	protected var _last_text = "";
	protected var _pending = false;

	public var BufferTimeout = 1000;
	public var MinBufferSize = 24000;
	public property Pending { get _pending };

	var stream_id;
	var _handle;
	var _vad;
	var _frame_size;

	var _end_voice_count = 0;
	var _dummy_buffer = "";

	WhisperSpeechStream(object owner) {
		this._owner = owner;
		this._handle = true;
		this._vad = fvad_new();
		fvad_set_sample_rate(this._vad, owner.modelSampleRate);
		this._frame_size = owner.modelSampleRate * 40 / 1000;
		this.stream_id = RandomInteger(0, 0x10000000);
	}

	protected filterVoice(var buffer) {
		var buf2 = "";
		for (var i = 0; i < length buffer; i += this._frame_size) {
			var frame = SubStr(buffer, i, this._frame_size);
			if (fvad_process(_vad, frame) == true) {
				_start_voice = true;
				_end_voice_count = 0;
				_end_voice = false;
				buf2 += frame;
			} else {
				if (_end_voice_count < 10)
					buf2 += frame;
				else
				if (length _dummy_buffer < (this._frame_size * 40))
					buf2 += frame;

				_end_voice_count ++;
			}
		}
		// one second with no voice
		if (_end_voice_count >= 48)
			_end_voice = true;

		return buf2;
	}

	feed(var full_buffer) {
		if (_pending)
			return;

		if (!_started)
			_started = true;

		var buffer = filterVoice(full_buffer);

		audio_buffer += buffer;

		var buf_size_ms = length audio_buffer / (_owner.modelSampleRate * 2) * 1000;

		if ((!_pending) && (audio_buffer) && (((_end_voice) && (buf_size_ms >= 1480)) || ((_start_voice) && (buf_size_ms >= BufferTimeout)))) {
			if (!_worker)
				_worker = _owner.findWorker(_worker_id);
			if (!_worker)
				throw "WhisperSpeechStream: No available workers";

			if (buf_size_ms < 1000)
				audio_buffer += _dummy_buffer;

			_worker.AddObject(new WhisperMessage(_worker_id, audio_buffer, stream_id));
			_pending = true;
			_started = true;

			audio_buffer = "";
			_dummy_buffer = "";

			_start_voice = 0;
			_end_voice = 0;

			_end_voice_count = 0;
		}
	}

	decode() {
		if (!_started)
			return "";

		if (!_worker)
			return "";

		_worker.GetResult(var data);
		if (data) {
			var obj = UnBinarizeObject(data);
			if ((obj) && (obj.stream_id == stream_id)) {
				_last_text = obj.buffer;
				_pending = false;
			}
		}
		return _last_text;
	}

	finish() {
		audio_buffer = "";
		_started = 0;
		_start_voice = 0;
		_end_voice = 0;
		_end_voice_count = 0;
		_last_text = "";
		_dummy_buffer = "";
		return "";
	}

	reset() {
		fvad_reset(_vad);
		this.finish();
		_pending = false;
		this.stream_id = RandomInteger(0, 0x10000000);
	}

	close() {
		audio_buffer = "";
		_dummy_buffer = "";
		_started = 0;
		_start_voice = 0;
		_end_voice = 0;
		_end_voice_count = 0;
		this._handle = false;
		this._owner.freeWorker(_worker_id);
	}

	finalize() {
		if (this._vad) {
			fvad_free(this._vad);
			this._vad = null;
		}
		if (this._owner)
			this._owner.freeWorker(_worker_id);
		this._handle = false;
		_started = 0;
	}
}

class WhisperSpeech {
	var _handle;

	var modelSampleRate = 16000;
	var maxWorkers = 0;

	var[] workers;
	var[] worker_busy;

	protected var _round_robin;

	WhisperSpeech(string language, model = "tiny", threads_per_worker = 1, whisper_parent = null, offline = false) {
		if (typeof whisper_parent == "class")
			whisper_parent = whisper_parent._handle;

		_handle = WhisperCreate(model, language, false, threads_per_worker, offline, whisper_parent);
		modelSampleRate = WhisperSampleRate();
	}

	freeWorker(id) {
		if ((id < 0) || (id >= length worker_busy))
			return;

		if (worker_busy[id] > 0)
			worker_busy[id] --;
	}

	findWorker(var id = -1) {
		id = -1;

		if (!this._handle)
			throw "Whisper context is not created";
		for (var i = 0; i < length worker_busy; i ++) {
			if (!worker_busy[i]) {
				worker_busy[i] = 1;
				id = i;
				return workers[i];
			}
		}

		var worker;

		if ((maxWorkers > 0) && (length workers >= maxWorkers)) {
			worker = workers[_round_robin];
			worker_busy[_round_robin] ++;
			id = _round_robin;
			_round_robin ++;
			return worker;
		}
		
		worker = new Worker("WhisperWorker", BinarizeObject([this._handle, length workers]));
		// length workers instead of length worker_busy (should be the same)
		worker_busy[length workers] = true;
		id = length workers;
		workers[length workers] = worker;

		return worker;
	}

	close() {
		if (!_handle)
			return;
		if (this.workers) {
			var msg = BinarizeObject(new WhisperMessage(-1));
			for (var i = 0; i < length workers; i ++) {
				workers[i].AddData(msg, 1);
				workers[i].Join();
			}
		}
		if (_handle)
			WhisperFree(_handle);
	}

	finalize() {
		this.close();
	}
}
