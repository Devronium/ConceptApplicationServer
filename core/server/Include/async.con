import standard.lang.cli
import standard.lang.serialize
import standard.C.time
import standard.C.io
import standard.lib.cripto
import standard.lib.mustache
include async.tin.con
include Loop.con
include Worker.con
include JWT.con

define ASYNC_CGI_TIMEOUT_S	3600

class __asynctimer {
	protected var timeout;
	protected var _delegate;
	public var onexception;

	__asynctimer(delegate, timeout) {
		this._delegate = delegate;
		this.timeout = microseconds() + timeout * 1000;
		Loop::add(this.iterate);
	}

	public cancel() {
		_delegate = null;
		timeout = 0;
	}

	protected iterate() {
		if (microseconds() >= timeout) {
			try {
				if (_delegate)
					_delegate();
			} catch (var exc) {
				if (onexception)
					onexception(exc);
			}
			return true;
		}
		return false;
	}
}

class async extends Loop {
	static timeout(delegate, timeout) {
		return new __asynctimer(delegate, timeout);
	}
}

class fs {
	protected var root_path = "";

	safepath(path) {
		if (root_path) {
			if ((path[0] != "/") && (path[0] != "\\") && (path[1] != ":"))
				path = root_path + path;
		}
		return path;
	}

	fs(basepath = "") {
		root_path = basepath;
	}

	readFile(path, callback) {
		if (callback) {
			var data = ReadFile(this.safepath(path));
			callback(_errno(), data);
		}
		return true;
	}

	writeFile(path, var data = "", callback = null) {
		var written = WriteFile(data, this.safepath(path));
		if (callback)
			callback(_errno());
		if (written)
			return true;
		return false;
	}

	appendFile(path, var data = "", callback = null) {
		var written = AppendFile(data, this.safepath(path));
		if (callback)
			callback(_errno());
		if (written)
			return true;
		return false;
	}
}

class accesstoken {
	var[] data;
	var setheader;
	var default_expires;

	protected var has_token;
	protected var privatekey;

	accesstoken(key, cookie = null) {
		if (cookie) {
			var token = cookie["token"];
			if (token) {
				var payload = JWT::validate(token, key);
				if (payload) {
					data = payload;
					this.has_token = true;
				}
			}
		}
		this.privatekey = key;
	}

	sync(expires = 2592000, path = "/", domain = "", samesite="", secure = false, httponly = true) {
		var expires_when = time() + expires;
		var sessiondata = data;
		sessiondata["exp"] = expires_when;
		var sessionid = JWT::build(sessiondata, this.privatekey);
		var cookie = "token=${sessionid}; Max-Age=" + expires;
		if (path)
			cookie += "; Path=$path";
		if (domain)
			cookie += "; Domain=$domain";
		if (samesite)
			cookie += "; SameSite=$samesite";
		if (secure)
			cookie += "; Secure";
		if (httponly)
			cookie += "; HttpOnly";
		if (this.setheader)
			this.setheader("Set-Cookie", cookie);

		return data;
	}
}

class session {
	var sessionid = "";
	var sessionpath;
	var setheader;
	var[] data;

	session() {
		this.sessionpath = _getenv("sessionpath");
		if (!this.sessionpath)
			this.sessionpath = _getenv("tmp");
		if (!this.sessionpath)
			this.sessionpath = _getenv("temp");
		if (!this.sessionpath)
			this.sessionpath = "/tmp";
	}

	operator[](key) {
		if ((data) && (IsSet(data, key)))
			return data[key];
		return null;
	}

	sessionfilename(session_id) {
		if ((Pos(session_id, ".") > 0) || (Pos(session_id, "/") > 0) || (Pos(session_id, "\\") > 0))
			session_id = "dummy";
		return sessionpath + "/" + sessionid + ".ess";
	}

	writesession(sessiondata, session_id) {
		return WriteFile(BinarizeObject(sessiondata), this.sessionfilename(session_id));
	}

	readsession(session_id) {
		var data = ReadFile(this.sessionfilename(session_id));
		if (data)
			return UnBinarizeObject(data);
		return null;
	}

	removesession(session_id) {
		_unlink(this.sessionfilename(session_id));
	}

	syncsession(array sessiondata) {
		if (sessionid) {
			try {
				if (typeof sessiondata == "array") {
					var info = sessiondata["session"];
					if (typeof info == "array") {
						var id = info["id"];	
						if (id == sessionid) {
							if (this.writesession(sessiondata, sessionid))
								return;
						}
					}
				}
			} catch (var exc) {
				console.error(LOG_THIS, exc);
			}
			this.removesession(sessionid);
		}
	}

	usesession(expires = 2592000, path = "/", domain = "", samesite = "", secure = false, httponly = true) {
		var sessiondata;
		if (sessionid)
			sessiondata = this.readsession(sessionid);
		if (sessiondata) {
			var info = sessiondata["session"];
			if (info) {
				var is_expired = info["expires"];
				if (time() > is_expired) {
					sessiondata = null;
					this.removesession(sessionid);
				}
			}
		}
		if (!sessiondata) {
			sessionid = sha256(CryptoRandom(80));
			var expires_when = time() + expires;
			sessiondata = ["session" => ["id" => sessionid, "expires" => expires_when, "created" => time()]];
			var cookie = "_ess=${sessionid}; Max-Age=" + expires;
			if (path)
				cookie += "; Path=$path";
			if (domain)
				cookie += "; Domain=$domain";
			if (samesite)
				cookie += "; SameSite=$samesite";
			if (secure)
				cookie += "; Secure";
			if (httponly)
				cookie += "; HttpOnly";
			if (this.setheader)
				this.setheader("Set-Cookie", cookie);
			this.writesession(sessiondata, sessionid);
		}

		data = sessiondata;
		return sessiondata;
	}
}

class Main {
	static Main() {
		GLOBALS()["context.async"] = true;
		var obj = CreateObject2("app", CLArg());
		if (obj)
			MainLoop();
		else
			console.error(LOG_THIS, "Cannot start: no app class defined");
	}
}

class _webrequest {
	var id;
	var env;
	var headers;
	var cookie;
	var GET;
	var POST;
	var web;
	var timestamp;
	var session;
	var token;
	var template = "";
	var lang = "";

	protected var _onpost;
	protected var _ondata;
	protected var _ontimedout;
	protected var _ondone;
	protected var _registered;
	protected var _onoutput;

	property onpost { get _onpost, set setonpost }
	property ondata { get _ondata, set setondata }
	property ontimedout { get _ontimedout, set setontimedout }
	property ondone { get _ondone, set setondone }
	property onoutput { get _onoutput, set setonoutput }

	_webrequest(owner, id, data, timestamp) {
		this.web = owner;
		this.timestamp = (timestamp ?? microseconds())/1000;
		web.requests++;
		this.id = id;
		this.env = data["env"];
		this.headers = data["headers"];
		this.cookie = data["cookies"];
		this.GET = data["GET"];
		this.POST = data["POST"];
	}


	csrf(webhost) {
		if (this.headers) {
			var host = "";
			if ((IsSet(this.headers, "referer")) && (this.headers["referer"])) {
				var re = /^((\w+):)?(\/\/((\w+)?(:(\w+))?@)?([^\/\?:]+)(:(\d+))?)?(\/?([^\/\?#][^\?#]*)?)?(\?([^#]+))?(#(\w*))?/;
				var arr = re.exec(this.headers["referer"]) ?? [ ];
				host = arr[8] ?? "";
			} else
			if (IsSet(this.headers, ":authority"))
				host = this.headers[":authority"];
			else
			if (IsSet(this.headers, "host"))
				host = this.headers["host"];

			// is a possible forgery?
			if ((host) && (webhost)) {
				host = ToLower(host);
				if (classof webhost == "RegExp") {
					if (webhost.test(host))
						return false;
				} else
				if (host == ToLower(webhost))
					return false;
			}
		}
		return true;
	}

	usesession(expires = 2592000, path = "/", domain = "", secure = false, httponly = true) {
		this.session = new session();
		this.session.setheader = this.header;
		if (this.cookie) {
			var ess = this.cookie["_ess"];
			if (ess)
				this.session.sessionid = ess;
		}
		this.session.usesession(expires, path, domain, secure, httponly);
		this.session.setheader = null;
	}

	usetoken(string key, expires = 2592000, path = "/", domain = "", secure = false, httponly = true) {
		this.token = new accesstoken(key, this.cookie);
	}

	header(key, val) {
		this.web.__buffer(["q" => id, "do" => "header", "data" => ["key" => key, "value" => val]], key);
	}

	done() {
		delete this._onpost;
		delete this._ondata;
		delete this._ontimedout;
		delete this._ondone;
		delete this._onoutput;

		this._registered = null;
		this.web.done(id);
		this.web.__buffer(["q" => id, "do" => "done"]);
	}

	print(data) {
		this.web.__buffer(["q" => id, "do" => "print", "data" => data]);
	}

	operator << (data) {
		this.print(data);
		return this;
	}

	flush() {
		this.web.flush();
	}

	protected addLangToPath(path) {
		if (!lang)
			return path;

		for (var i = length path - 1; i >= 0; i --) {
			var e = path[i];
			if ((e == "/") || (e == "\\")) {
				path[i] = path[i] + lang + e;
				return path;
			}
		}

		return lang + "/" + path;
	}

	protected templateData(template) {
		var fullpath;

		if (lang) {
			var template_lang = this.addLangToPath(template);
			if (template_lang[0] != "/")
				fullpath = this.web.fs.safepath(this.web.path) + template_lang;
			else
				fullpath = template_lang;
			if (FileExists(fullpath))
				return ReadFile(fullpath);
		}

		if (template[0] != "/")
			fullpath = this.web.fs.safepath(this.web.path) + template;
		else
			fullpath = template;

		return ReadFile(fullpath);
	}

	put(array data) {
		if (template) {
			var tpl = this.templateData(this.template);
			if (tpl) {
				var str = mustache(tpl, data, var err, function(template) {
					var tpl = this.templateData(template);
					if (tpl)
						return tpl;
				});
				if (err) {
					switch (err) {
						case MUSTACH_ERROR_SYSTEM:
							this.print("MUSTACH_ERROR_SYSTEM in template");
							break;
						case MUSTACH_ERROR_UNEXPECTED_END:
							this.print("MUSTACH_ERROR_UNEXPECTED_END in template");
							break;
						case MUSTACH_ERROR_EMPTY_TAG:
							this.print("MUSTACH_ERROR_EMPTY_TAG in template");
							break;
						case MUSTACH_ERROR_TAG_TOO_LONG:
							this.print("MUSTACH_ERROR_TAG_TOO_LONG in template");
							break;
						case MUSTACH_ERROR_BAD_SEPARATORS:
							this.print("MUSTACH_ERROR_BAD_SEPARATORS in template");
							break;
						case MUSTACH_ERROR_TOO_DEEP:
							this.print("MUSTACH_ERROR_TOO_DEEP in template");
							break;
						case MUSTACH_ERROR_CLOSING:
							this.print("MUSTACH_ERROR_CLOSING in template");
							break;
						case MUSTACH_ERROR_BAD_UNESCAPE_TAG:
							this.print("MUSTACH_ERROR_BAD_UNESCAPE_TAG in template");
							break;
						case MUSTACH_ERROR_INVALID_ITF:
							this.print("MUSTACH_ERROR_INVALID_ITF in template");
							break;
						case MUSTACH_ERROR_ITEM_NOT_FOUND:
							this.print("MUSTACH_ERROR_ITEM_NOT_FOUND in template");
							break;
						case MUSTACH_ERROR_PARTIAL_NOT_FOUND:
							this.print("MUSTACH_ERROR_PARTIAL_NOT_FOUND in template");
							break;
						case MUSTACH_ERROR_UNDEFINED_TAG:
							this.print("MUSTACH_ERROR_UNDEFINED_TAG in template");
							break;
						default:
							this.print("UNKNOWN ERROR #$err in template");
					}
					return null;
				}
				data = str;
			}
		}
		this.print(data);
		return this;
	}

	sync(expires = 2592000, path = "/", domain = "", samesite = "", secure = false, httponly = true) {
		if (this.session)
			this.session.syncsession(this.session.data);
		if (this.token) {
			this.token.setheader = this.header;
			this.token.sync(this.token.default_expires ?? expires, path, domain, samesite, secure, httponly);
			this.token.setheader = null;
		}
	}

	register() {
		if (!_registered) {
			this.web.register(id, this);
			_registered = true;
		}
	}

	unregister() {
		if (_registered) {
			this.web.done(id);
			_registered = false;
		}
	}

	setonpost(deleg) {
		this.register();
		this._onpost = deleg;
		return deleg;
	}

	setondata(deleg) {
		this.register();
		this._ondata = deleg;
		return deleg;
	}

	setontimedout(deleg) {
		this.register();
		this._ontimedout = deleg;
		return deleg;
	}

	setondone(deleg) {
		this.register();
		this._ondone = deleg;
		return deleg;
	}

	setonoutput(deleg) {
		this.register();
		this._onoutput = deleg;
		return deleg;
	}

	finalize() {
		this.sync();
		web.requests--;
		done();
		console.info(LOG_THIS, "Request fulfilled in ${microseconds()/1000 - this.timestamp}ms");
	}
}

class web {
	var[] cache;
	var[] requestdata;
	var requests;
	var drop;
	var db;
	var userdata;
	var path;
	var[] buffercache;
	var[] headercache;
	var idle;
	var timeout = 3600;
	var fs;
	protected var lazy_call;

	withdb(delegate, string username = "sysdbadministrator", string password = "sysdbadministrator", string db = "appdb", string host = "localhost", number port = 2668) {
		if (this.db) {
			if (this.db.ifauth()) {
				delegate();
				return true;
			}
			return false;
		}
		this.lazy_call = delegate;
		this.db = new tinasync(db, host, port);
		this.db.auth(username, password).then({
			this.db.keepopen();
			if (this.lazy_call) {
				this.lazy_call();
				delete this.lazy_call;
			}
		});
		return true;
	}

	connect(string username = "sysdbadministrator", string password = "sysdbadministrator", string db = "appdb", string host = "localhost", number port = 2668) {
		if (this.db) {
			if (this.db.ifauth())
				return true;
			return false;
		}

		this.db = new tinasync(db, host, port);
		this.db.keepopen();
		return this.db.auth(username, password);
	}

	sql(query, params = null) {
		if ((!this.db) && (!this.db.ifauth()))
			throw "Not connected/authorized";
		return this.db.sql(query, params);
	}

	done(id) {
		if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
			requestdata[id] = null;
			if ((length requestdata - requests) > 100) {
				var new_requests = new [];
				var keys = GetKeys(requestdata);
				for (var i = 0; i < length keys; i++) {
					var k = keys[i];
					if (k) {
						var e = this.requestdata[k];
						if (e)
							new_requests[k] = e;
					}
				}
				this.requestdata = new_requests;
			}
		}
	}

	register(id, obj) {
		if ((id) && (typeof id == "string") && (obj))
			requestdata[id] = obj;
	}

	dispatch(data) {
		var obj;
		var id = data["q"];
		switch (data["do"]) {
			case "new":
				CreateObject2("app", new _webrequest(this, id, data["data"], data["_"]));
				break;
			case "post":
				if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
					obj = requestdata[id];
					if ((obj) && (obj.onpost))
						obj.onpost(data["data"]);
				}
				break;
			case "data":
				if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
					obj = requestdata[id];
					if ((obj) && (obj.ondata))
						obj.ondata(data["data"]);
				}
				break;
			case "drop":
				console.debug(LOG_THIS, "drop request for ${this.path}");
				requestdata = new [];
				drop = true;
				QuitLoop();
				return true;
			case "timedout":
				if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
					obj = requestdata[id];
					if ((obj) && (obj.ontimedout))
						obj.ontimedout();
				}
				break;
			case "done":
				if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
					obj = requestdata[id];
					if ((obj) && (obj.ondone))
						obj.ondone();
				}
				this.done(id);
				break;
			case "output":
				if ((id) && (typeof id == "string") && (IsSet(requestdata, id))) {
					obj = requestdata[id];
					if ((obj) && (obj.onoutput))
						obj.onoutput();
				}
				break;
			default:
				this.done(id);
				break;
		}
		return false;
	}

	flush() {
		if ((buffercache) || (headercache)) {
			if (headercache) {
				Worker::Result(BinarizeObject(headercache + buffercache));
				headercache = new [];
			} else
				Worker::Result(BinarizeObject(buffercache));

			buffercache = new [];
		}
	}

	__buffer(data, prepend_key = null) {
		if (prepend_key)
			headercache[prepend_key] = data;
		else
			buffercache[length buffercache] = data;

		if ((length buffercache + length headercache) > 100)
			this.flush();
	}

	iterate() {
		var timeout = 0;
		if (drop) {
			QuitLoop();
			return true;
		}

		if (requests) {
			idle = 0;
		} else {
			timeout = 1000;
			idle++;
		}

		this.flush();
		if (Worker::Pending(var data_arr, timeout)) {
			idle = 0;
			if (data_arr) {
				data_arr = UnBinarizeObject(data_arr);
				for (var i = 0; i < length data_arr; i++) {
					var data = data_arr[i];

					if ((!data) || (dispatch(data)))
						return false;
				}
			} else
				return false;

			if (!requests)
				timeout = 1000;
		}
		if ((timeout) && (idle >= ASYNC_CGI_TIMEOUT_S)) {
			console.debug(LOG_THIS, "idle drop for ${this.path}");
			this.drop = true;
			requestdata = new [];
			QuitLoop();
			return true;
		}
		return false;
	}

	static exit() {
		console.info(LOG_THIS, "Worker closed");
		Worker::Result(BinarizeObject([["do" => "drop"]]));
		QuitLoop();
	}

	web(data = "") {
		this.path = data;
		this.fs = new fs(data);
		Loop::add(iterate);
		GLOBALS()["context.async"] = true;
		if (this.path)
			GLOBALS()["context.root"] = this.path;
		MainLoop();
		this.exit();
	}
}
