include Serializable.con
include HTTPRequest.con

import standard.lib.str

class XMLAPI {
	protected var[] __properties;

	attribute(name, v) {
		__properties[name] = v;
		return this;
	}

	static __xmlsafe(str) {
		if (typeof str == "string") {
			str = StrReplace(str, "&", "&amp;");
			str = StrReplace(str, "<", "&lt;");
			str = StrReplace(str, ">", "&gt;");
			str = StrReplace(str, "\"", "&quot;");
			str = StrReplace(str, "'", "&apos;");
		}
		return str;
	}

	protected static __toproperties(arr) {
		var properties = "";
		if (!arr)
			return properties;
		var keys = GetKeys(arr);
		for (var i = 0; i < length keys; i++) {
			var k = keys[i];
			if (k) {
				var v = arr[k];
				switch (typeof v) {
					case "string":
						properties += " $k=\"${XMLAPI::__xmlsafe(v)}\"";
						break;
					case "numeric":
						properties += " $k=\"$v\"";
						break;
				}
			}
		}
		return properties;
	}

	protected static valuetoxml(v, k, levelstr = "") {
		var xmlstr = "";
		// if (Pos(k, "xmlns_") == 1)
		// 	k[5] = ":";
		// else
		// if (Pos(k, "xsi_") == 1)
		// 	k[3] = ":";
		switch (typeof v) {
			case "string":
				xmlstr += "$levelstr<$k>" + XMLAPI::__xmlsafe(v) + "</$k>\n";
				break;
			case "numeric":
				xmlstr += "$levelstr<$k>$v</$k>\n";
				break;
			case "array":
				var keys = GetKeys(v);
				if ((keys) && (keys[0]))
					xmlstr += XMLAPI::toxml(v, null, k, levelstr);
				else
					xmlstr += XMLAPI::arraytoxml(v, k, levelstr);
				break;
			case "class":
				if (Pos(k, "__") == 1) {
					xmlstr += v.xml("", false, levelstr);
				} else {
					xmlstr += "$levelstr<$k>\n";
					xmlstr += v.xml("", false, levelstr + "\t");
					xmlstr += "$levelstr</$k>\n";
				}
				break;
		}
		return xmlstr;
	}

	protected static arraytoxml(array arr, nodename, levelstr) {
		var xmlstr = "";
		for (var i = 0; i < length arr; i++)
			xmlstr += XMLAPI::valuetoxml(arr[i], nodename, levelstr);
		return xmlstr;
	}

	static toxml(array arr, properties, nodename, levelstr = "") {
		var keys = GetKeys(arr);
		var xmlstr = "";

		xmlstr += "$levelstr<$nodename${XMLAPI::__toproperties(properties)}";
		var xml_content = "";
		for (var i = 0; i < length keys; i++) {
			var k = keys[i];
			if ((k) && (k != "__properties") && (k != "UserData"))
				xml_content += XMLAPI::valuetoxml(arr[k], k, levelstr + "\t");
		}
		if (xml_content)
			xmlstr += ">\n" + xml_content + "$levelstr</$nodename>\n";
		else
			xmlstr += " />\n";
		return xmlstr;
	}

	xml(nodename = "", add_xml_encoding = true, levelstr = "") {
		var xmlstr = "";
		if (add_xml_encoding)
			xmlstr += "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n";
		xmlstr += toxml(ToArray(this, false, true), __properties, nodename ?? classof this, levelstr);
		return xmlstr;
	}

	post(url, callback = null, timeout = 5000) {
		var request = new HTTPRequest(url);
		var xml_data = this.xml("", false);
		var headers = ["Content-Type" => "application/xml", "Content-Length" => length xml_data];
		request.OnResponse = callback;
		if (request.request("POST", headers, xml_data))
			request.iterate(timeout);
		else
			return -1;

		return request.response["code"];
	}
}
